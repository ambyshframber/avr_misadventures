LOAD ../m328P.def

0 B,

DECIMAL

: SPI_INIT
	{ BIN 00101100 DEC } 16 LDI \ set SCK, MOSI and /SS as outputs
	16 DDRB OUT

	{ BIN 01010010 DEC } 16 LDI \ config spi
	16 SPCR OUT
;

: SPI_XFER
	16 SPDR OUT
	BEGIN
		SPSR 16 IN
		16 7 SET STBR
	AGAIN
	SPDR 16 IN
;

: UART_INIT
	\ init uart to 9600 baud
	103 16 LDI
	16 UBRR0L STS
	0 16 LDI
	16 UBRR0H STS

	{ 1 RXEN0 << 1 TXEN0 << OR } 16 LDI
	16 UCSR0B STS
;

: EMIT
	BEGIN
		UCSR0A 17 LDS
		17 UDRE0 SET STBR
	AGAIN

	16 UDR0 STS
;

: KEY
	BEGIN
		UCSR0A 16 LDS
		16 RXC0 SET STBR
	AGAIN
	UDR0 16 LDS
;

: TOHEX
	10 16 CPI
	CF SET IF
		{ CHAR A CHAR 9 1+ - } 16 ADDI
	THEN
	{ CHAR 0 } 16 ADDI
;
: PUTHEX
	16 NSWAP
	16 18 MOV
	15 16 ANDI
	TOHEX EMIT

	18 NSWAP
	18 16 MOV
	15 16 ANDI
	TOHEX EMIT
;

: FROMHEX
	{ CHAR 9 1+ } 16 CPI
	CF SET IF
		{ 1 5 << INVERT } 16 ANDI \ case insensitive
		{ CHAR A CHAR 9 1+ - } 16 SUBI
	THEN
	{ CHAR 0 } 16 SUBI
;
: GETHEX
	KEY FROMHEX
	16 17 MOV
	17 NSWAP
	KEY FROMHEX
	17 16 OR
;

: RESET
	UART_INIT
	SPI_INIT

	{ BIN 1100 DEC } 16 LDI
	16 DDRD OUT

	BEGIN
		GETHEX
		EMIT
		PUTHEX
		
		SPI_XFER
		PORTD 2 SBI
		PORTD 2 CBI
	AGAIN

' RESET RESET!

FINISH
